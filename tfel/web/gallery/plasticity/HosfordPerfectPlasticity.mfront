@DSL       Implicit;
@Behaviour HosfordPerfectPlasticity;
@Author    Thomas Helfer;
@Description{
  A simple implementation of a perfect
  plasticity behaviour using the
  Hosford stress.
};

@ModellingHypotheses {".+"};

@Brick StandardElasticity;
@Epsilon 1.e-16;
@Theta 1;

@StateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");

@ElasticMaterialProperties {150e9,0.3};
@Parameter a = 8;
@Parameter sigy = 150e6;

@LocalVariable bool b;

@InitializeLocalVariables{
  const stress seps = 1.e-10*young;
  const auto sigel = computeElasticPrediction();
  const auto seqel = computeHosfordStress(sigel,a,seps);
  b = seqel>sigy;
}

@Integrator{
  const stress seps = 1.e-10*young;
  if(!b){
    // elastic loading, nothing to be done
    return true;
  }
  real seq;
  Stensor n;
  Stensor4 dn;
  std::tie(seq,n,dn) = computeHosfordStressSecondDerivative(sig,a,seps);
  feel        += dp*n;
  dfeel_ddeel += 2*mu*theta*dp*dn;
  dfeel_ddp    = n;
  fp           = (seq-sigy)/young;
  dfp_ddeel    = 2*mu*theta*n/young;
  // this is a small trick that is necessary if the first time
  // step leads to a plastic loading:
  // on this case, the stress are zero on the first iteration and
  // this results in a null line in the jacobian matrix because
  // the normal is then undefined.
  // To avoid the issue, we arbitrarily set dfp_ddp to 1 in that case. 
  dfp_ddp      = (seq<seps) ? 1:  0;
}
